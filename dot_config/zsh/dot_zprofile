# ~/.config/zsh/.zprofile
# Purpose:
# - Decide NIX_PROFILE_PREFIX (per-user profile preferred for zsh plugins layout)
# - Initialize Homebrew env via `brew shellenv` (prefer nix-homebrew launcher)
# - Finally enforce PATH order: Nix first, Homebrew later (cask-only policy)

typeset -U path

# Seed PATH (keep it minimal; further ordering is enforced at the end)
path=(
  "$HOME/.local/bin"
  # "$HOME/bin"
  "/etc/profiles/per-user/$USER/bin"
  "/run/current-system/sw/bin"
  $path
)

# Choose the profile prefix by checking where the plugin layout actually exists.
if [[ -d "/etc/profiles/per-user/$USER/share/zsh/plugins" ]]; then
  export NIX_PROFILE_PREFIX="/etc/profiles/per-user/$USER"
elif [[ -d "$HOME/.nix-profile/share/zsh/plugins" ]]; then
  export NIX_PROFILE_PREFIX="$HOME/.nix-profile"
else
  unset NIX_PROFILE_PREFIX
fi

# Homebrew environment
# Prefer nix-homebrew unified launcher if present.
if [[ -x /run/current-system/sw/bin/brew ]]; then
  eval "$(/run/current-system/sw/bin/brew shellenv)"
elif [[ -x /opt/homebrew/bin/brew ]]; then
  eval "$(/opt/homebrew/bin/brew shellenv)"
fi

# --- Final PATH order fix (prefer Nix, keep Homebrew later) ---

# Put Nix profile first
if [[ -n "${NIX_PROFILE_PREFIX:-}" && -d "${NIX_PROFILE_PREFIX}/bin" ]]; then
  path=("${path[@]:#${NIX_PROFILE_PREFIX}/bin}")
  path=("${NIX_PROFILE_PREFIX}/bin" $path)
fi

# Push Homebrew behind Nix (brew is cask-only in this setup)
path=("${path[@]:#/opt/homebrew/bin}")
path=("${path[@]:#/opt/homebrew/sbin}")
path+=("/opt/homebrew/bin" "/opt/homebrew/sbin")

typeset -U path
export PATH
